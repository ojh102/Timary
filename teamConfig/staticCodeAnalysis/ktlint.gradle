def deployDir = System.getenv("BITRISE_DEPLOY_DIR") ?: "$projectDir/build"

repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:$ktlint_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = 'com.pinterest.ktlint.Main'

    args "src/**/*.kt", "--reporter=plain", "--reporter=checkstyle,output=$deployDir/reports/ktlint/ktlint.xml"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style violations."
    classpath = configurations.ktlint
    main = 'com.pinterest.ktlint.Main'
    args "-F", "src/**/*.kt"
}